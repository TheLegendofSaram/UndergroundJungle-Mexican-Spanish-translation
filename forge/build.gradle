import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    // Required by Forge
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    // Forge gradle plugin
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    // Mixin plugin
    id 'org.spongepowered.mixin' version "0.7.+"
    // CurseForge publishing
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    // Modrinth publishing
    id "com.modrinth.minotaur" version "2.+"
}

// Archive name for the forge mod
base {
    archivesName = 'underground-jungle-forge'
}

// Minecraft configuration
minecraft {
    // Use official mappings
    mappings channel: 'official', version: minecraft_version
    // Required by Forge MDK
    copyIdeResources = true
    // Forge run configurations
    runs {
        client {
            workingDirectory project.file('../.minecraft')
            ideaModule "${rootProject.name}.${project.name}.main"
            mods {
                "${modId}" {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    // Minecraft forge dependency
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    // Common module dependency
    implementation project(':common')
    // Mixin annotation processor
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    // Optional TerraBlender dependency
    implementation fg.deobf("com.github.glitchfiend:TerraBlender-forge:${minecraft_version}-${terra_blender_version}")
}

mixin {
    // Specify mixin refmap
    add sourceSets.main, "mixin.undergroundjungle.refmap.json"
    // Common module mixin file
    config 'mixin.undergroundjungle.json'
    // Forge module mixin file
    config 'mixin.undergroundjungle.forge.json'
}

// Copy sources from the common module
tasks.withType(JavaCompile).configureEach {
    source project(':common').sourceSets.main.allSource
}

// Copy resources from the common module
processResources {
    from project(':common').sourceSets.main.resources
}

// Re-obfuscate the jar
jar {
    finalizedBy 'reobfJar'
}

// Read the changelog file
var changelogFile = rootProject.file('changelog.md')
var changelogText = changelogFile.exists() ? changelogFile.text : ''

// Publish to CurseForge
tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = System.getenv('CURSEFORGE_TOKEN')
    upload('918314', jar) {
        changelogType = 'markdown'
        changelog = changelogText
        releaseType = 'release'
        displayName = mod_name + ' - ' + mod_version + ' Forge'
        addGameVersion minecraft_version
        addModLoader 'Forge'
    }
}

// Publish to Modrinth
modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'underground-jungle'
    versionNumber = mod_version
    versionName = mod_name + ' - ' + mod_version + ' Forge'
    versionType = 'release'
    uploadFile = jar
    gameVersions = [minecraft_version]
    loaders = ['forge']
    changelog = changelogText
}