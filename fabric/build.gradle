import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    // Required by Fabric loom
    id 'java'
    id 'idea'
    id 'maven-publish'
    // Fabric plugin
    id 'fabric-loom' version '1.6-SNAPSHOT'
    // CurseForge publishing
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    // Modrinth publishing
    id "com.modrinth.minotaur" version "2.+"
}

// Archive name for the fabric mod
base {
    archivesName = 'underground-jungle-fabric'
}

loom {
    // Specify mixin refmap
    mixin {
        defaultRefmapName.set "mixin.undergroundjungle.refmap.json"
    }
    // Fabric run configurations
    runs {
        client {
            client()
            ideConfigGenerated(true)
            runDir('../.minecraft')
        }
    }
}

dependencies {
    // Minecraft dependency
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    // Fabric loader and api dependency
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}+${minecraft_version}"
    // Common module dependency
    implementation project(':common')
    // Optional TerraBlender dependency
    modCompileOnly "com.github.glitchfiend:TerraBlender-fabric:${minecraft_version}-${terra_blender_version}"
}

// Copy sources from the common module
tasks.withType(JavaCompile).configureEach {
    source project(':common').sourceSets.main.allSource
}

// Copy resources from the common module
processResources {
    from project(':common').sourceSets.main.resources
}

// Read the changelog file
var changelogFile = rootProject.file('changelog.md')
var changelogText = changelogFile.exists() ? changelogFile.text : ''

// Publish to CurseForge
tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = System.getenv('CURSEFORGE_TOKEN')
    upload('918314', jar) {
        changelogType = 'markdown'
        changelog = changelogText
        releaseType = 'release'
        displayName = mod_name + ' - ' + mod_version + ' Fabric'
        addRequirement 'fabric-api'
        addModLoader 'Fabric'
        addGameVersion minecraft_version
    }
}

// Publish to Modrinth
modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'underground-jungle'
    versionNumber = mod_version
    versionName = mod_name + ' - ' + mod_version + ' Fabric'
    versionType = 'release'
    uploadFile = remapJar
    gameVersions = [minecraft_version]
    loaders = ['fabric']
    changelog = changelogText
    dependencies {
        required.project 'fabric-api'
    }
}